import java.util.regex.Matcher

apply plugin: 'com.android.library'

def versionName() {
    String config = 'library/src/main/java/com/qiniu/android/common/Constants.java'
    String fileContents = new File(config).text
    Matcher myMatcher = fileContents =~ /VERSION = "(.+)";/
    String version = myMatcher[0][1]
    println(version)
    return version
}

def versionNameToCode(String version) {
    String v = version.replaceAll(/\./, '')
    return v.toLong()
}

String version = versionName()
int code = versionNameToCode(version)

android {
    compileSdkVersion 26
    buildToolsVersion '25.0.2'
    defaultConfig {
        //applicationId "com.qiniu.android"
        minSdkVersion 9
        targetSdkVersion 26
        versionCode code
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }

    }

    lintOptions {
        warning 'InvalidPackage'
    }
}

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.12.6'
    // for javax.annotation.Nullable use in custom MultipartBody and Headers implements.
//    compile 'com.google.code.findbugs:jsr305:3.0.2'
    compile fileTree(include: ['*.jar'], dir: 'libs')
}

task releaseJar(type: Jar, dependsOn: 'build') {
    //指定生成的jar名
    baseName 'qiniu-android-sdk-' + version
    //从哪里打包class文件
//    from('build/intermediates/classes/release/com/qiniu/android/dns/')
    //打包到jar后的目录结构
//    into('com/qiniu/android/dns/')
    //去掉不需要打包的目录和文件
    exclude('test/', 'BuildConfig.class', 'R.class')
    //去掉R$开头的文件
    exclude { it.name.startsWith('R$'); }
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    task.exclude '**/R.*', '**/R$*.*', '**/BuildConfig.class'
    artifacts.add('archives', task);
}

setProperty('VERSION_NAME', version)
setProperty('VERSION_CODE', code)

apply from: '../mvn_push.gradle'
