import java.util.regex.Matcher

apply plugin: 'com.android.library'

def versionName() {
    String config = 'collect/src/main/java/com/qiniu/android/collect/Constants.java'
    String fileContents = new File(config).text
    Matcher myMatcher = fileContents =~ /VERSION = "(.+)";/
    String version = myMatcher[0][1]
    println(version)
    return version
}

def versionNameToCode(String version) {
    String v = version.replaceAll(/\./, '')
    return v.toLong()
}

String version = versionName()
int code = versionNameToCode(version)

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'
    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode code
        versionName version
    }

    lintOptions {
        warning 'InvalidPackage'
    }
}

dependencies {
//    compile 'com.qiniu:qiniu-android-sdk:7.3.+'
    compile project(':library')
}

task releaseJar(type: Jar, dependsOn: 'build') {
    //指定生成的jar名
    baseName 'qiniu-android-collect-sdk-' + version
    //从哪里打包class文件
    from('build/intermediates/classes/release/com/qiniu/android/collect/')
    //打包到jar后的目录结构
    into('com/qiniu/android/collect/')
    //去掉不需要打包的目录和文件
    exclude('test/', 'BuildConfig.class', 'R.class')
    //去掉R$开头的文件
    exclude { it.name.startsWith('R$'); }
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    task.exclude '**/R.*', '**/R$*.*', '**/BuildConfig.class'
    artifacts.add('archives', task);
}

setProperty('VERSION_NAME', version)
setProperty('VERSION_CODE', code)

apply from: '../mvn_push.gradle'